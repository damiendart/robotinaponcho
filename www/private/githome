#!/usr/bin/perl -w
# Git Repositories page generation script for Damien Dart's personal website.
#
# Copyright (C) 2013-2016 Damien Dart, <damiendart@pobox.com>.
# This file is distributed under the MIT licence. For more information,
# please refer to the accompanying "LICENCE" file.

use strict;

use File::Basename;
use HTML::Template;
use URI;

if (@ARGV < 2 ) {
  print("Not enough operands\n");
  print("USAGE: " . basename($0) . " REPOS-FOLDER HTML-TEMPLATE-FILE\n");
  exit(2);
}
opendir(my $directory, $ARGV[0]) or die "Unable to open directory.";
my @repositories = grep { !/^\.\.?/ && /\.git$/ } readdir($directory);
closedir($directory);
my @table_data;
foreach (@repositories) {
  (my $repository = "$ARGV[0]/$_") =~ s|//|/|;
  chdir $repository;
  chomp(my $description = `git config --get gitweb.description`);
  $description =~ s/'/&rsquo;/g;
  my %row = ( description => $description,
      last_commit => `git log -1 --pretty=format:%ci`, repository => $_, 
      timestamp => `git log -1 --pretty=format:%ct`, 
      uri => URI->new("gitweb.cgi?p=$_"));
  push(@table_data, \%row);
}
my $template = HTML::Template->new(filename => $ARGV[1]);
$template->param(repositories => \@table_data);
my $output = $template->output;
$output =~ s/^\s+//;
$output =~ s/\n\s*\n/\n/g;
print($output);

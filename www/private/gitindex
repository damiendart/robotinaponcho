#!/usr/bin/perl -w
# Git Repositories page generation script for Damien Dart's personal website.
#
# Copyright (C) 2013-2016 Damien Dart, <damiendart@pobox.com>.
# This file is distributed under the MIT licence. For more information,
# please refer to the accompanying "LICENCE" file.

use strict;

use File::Basename;
use File::pushd;
use File::Spec::Functions qw/catfile rel2abs/;
use HTML::Template;
use Text::Typography qw/typography/;

sub output
{
  my $output = $_[0]->output;
  open(my $filehandle, ">", $_[1]) or die "Unable to open file for writing";
  print($filehandle $output);
  close($output);
  system("gzip -9c $_[1] > $_[1].compressed");
}

if (@ARGV < 1 ) {
  print(STDERR "Not enough operands\n");
  print(STDERR "USAGE: " . basename($0) . " REPOS-FOLDER\n");
  exit(2);
}
my $git_template = HTML::Template->new(
    filename => (rel2abs(__FILE__) . ".tmpl"), die_on_bad_params => 0);
my $repo_template = HTML::Template->new(
    filename => (rel2abs(__FILE__) . ".repo.tmpl"), die_on_bad_params => 0);
my @table_data;
opendir(my $directory, $ARGV[0]) or die "Unable to open directory.";
my @repositories = grep { !/^\.\.?/ && /\.git$/ } readdir($directory);
closedir($directory);
foreach (sort { lc($a) cmp lc($b) } @repositories) {
  my $dir = pushd(catfile($ARGV[0], $_));
  chomp(my $description = `git config --get gitweb.description`);
  chomp(my $title = `git show -s --format=%s`);
  my %repo_data = (description => typography($description),
      github_url => "https://github.com/damiendart/" . basename($_, ".git"),
      hash_short => `git log -1 --pretty=format:%h`,
      hash_long => `git log -1 --pretty=format:%H`,
      last_commit => `git log -1 --pretty=format:%ci`, repository => $_,
      last_commit_title => typography($title),
      timestamp =>`git log -1 --pretty=format:%ct`, uri => "$_/");
  push(@table_data, \%repo_data);
  $repo_template->param(\%repo_data);
  output($repo_template, "index.html");
}
$git_template->param(repositories => \@table_data);
output($git_template, catfile($ARGV[0], "index.html"));

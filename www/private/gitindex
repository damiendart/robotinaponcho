#!/usr/bin/perl -w
# Git Repositories page generation script for Damien Dart's personal website.
#
# Copyright (C) 2013-2016 Damien Dart, <damiendart@pobox.com>.
# This file is distributed under the MIT licence. For more information,
# please refer to the accompanying "LICENCE" file.

use strict;

use File::Basename;
use File::pushd;
use File::Spec::Functions qw/catfile rel2abs/;
use HTML::Template;
use URI;

if (@ARGV < 1 ) {
  print(STDERR "Not enough operands\n");
  print(STDERR "USAGE: " . basename($0) . " REPOS-FOLDER\n");
  exit(2);
}
opendir(my $directory, $ARGV[0]) or die "Unable to open directory.";
my @repositories = grep { !/^\.\.?/ && /\.git$/ } readdir($directory);
closedir($directory);
my @table_data;
foreach (sort { lc($a) cmp lc($b) } @repositories) {
  my $dir = pushd(catfile($ARGV[0], $_));
  chomp(my $description = `git config --get gitweb.description`);
  $description =~ s/'/&rsquo;/g;
  my %row = ( description => $description,
      last_commit => `git log -1 --pretty=format:%ci`, repository => $_, 
      timestamp => `git log -1 --pretty=format:%ct`, 
      uri => URI->new("gitweb.cgi?p=$_"));
  push(@table_data, \%row);
}
my $template = HTML::Template->new(filename => (rel2abs(__FILE__) . ".tmpl"));
$template->param(repositories => \@table_data);
my $output = $template->output;
$output =~ s/^\s+//;
$output =~ s/\n\s*\n/\n/g;
open(my $filehandle, ">", catfile($ARGV[0], "index.html"))
    or die "Unable to open file for writing";
print($filehandle $output);
close($output);
system("gzip -9c " . catfile($ARGV[0], "index.html") . " > " .
    catfile($ARGV[0], "index.html.compressed"));
